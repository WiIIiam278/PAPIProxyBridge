import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '8.3.6'
    id 'org.cadixdev.licenser' version '0.6.1' apply false
    id 'dev.architectury.loom' version '1.9-SNAPSHOT' apply false
    id 'gg.essential.multi-version.root' apply false
    id 'org.ajoberstar.grgit' version '5.3.0'
    id 'maven-publish'
    id 'java'
}

group 'net.william278'
version "$ext.plugin_version${versionMetadata()}"
description "$ext.plugin_description"
defaultTasks 'licenseFormat', 'build'

ext {
    set 'version', version.toString()
    set 'description', description.toString()
}

publishing {
    repositories {
        if (System.getenv("RELEASES_MAVEN_USERNAME") != null) {
            maven {
                name = "william278-releases"
                url = "https://repo.william278.net/releases"
                credentials {
                    username = System.getenv("RELEASES_MAVEN_USERNAME")
                    password = System.getenv("RELEASES_MAVEN_PASSWORD")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        if (System.getenv("SNAPSHOTS_MAVEN_USERNAME") != null) {
            maven {
                name = "william278-snapshots"
                url = "https://repo.william278.net/snapshots"
                credentials {
                    username = System.getenv("SNAPSHOTS_MAVEN_USERNAME")
                    password = System.getenv("SNAPSHOTS_MAVEN_PASSWORD")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

allprojects {
    if (project.name == 'fabric') {
        return
    }

    apply plugin: 'com.gradleup.shadow'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'java'

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.release.set 17
    javadoc.options.encoding = 'UTF-8'
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.fabricmc.net/' }
        // Fabric maven is above paper maven to get FabricAPi from it over PaperMC
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.38'
        annotationProcessor 'org.projectlombok:lombok:1.18.38'

        testCompileOnly 'org.projectlombok:lombok:1.18.38'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.12.1'
    }

    test {
        useJUnitPlatform()
    }

    license {
        header = rootProject.file('HEADER')
        include '**/*.java'
        newLine = true
    }

    processResources {
        filesMatching(['**/*.json', '**/*.yml']) {
            filter ReplaceTokens as Class, beginToken: '${', endToken: '}',
                    tokens: rootProject.ext.properties
        }
    }

    jar {
        manifest {
            attributes 'Implementation-Version': version
        }
    }
}

subprojects {
    // Ignore root fabric project (no jars)
    if (project.name == 'fabric') {
        return
    }

    version rootProject.version
//    archivesBaseName = "${rootProject.name}-${project.name.capitalize()}"
    def name = "$rootProject.name"
    if (rootProject != project.parent) {
        name += "-${project.parent.name.capitalize()}"
    } else {
        name += "-${project.name.capitalize()}"
    }
    archivesBaseName = name

    if (project.parent?.name?.equals('fabric')) {
        apply plugin: 'dev.architectury.loom'
        compileJava.options.release.set (project.name == '1.20.1' ? 17 : 21) // 1.20.1 requires Java 17
        version += "+mc.${project.name}"
    }

    jar {
        from '../LICENSE'
    }

    shadowJar {
        destinationDirectory.set(file("$rootDir/target"))
        archiveClassifier.set('')
    }

    if (['common'].contains(project.name)) {
        java {
            withSourcesJar()
            withJavadocJar()
        }

        sourcesJar {
            destinationDirectory.set(file("$rootDir/target"))
        }
        javadocJar {
            destinationDirectory.set(file("$rootDir/target"))
        }
        jar.dependsOn(sourcesJar, javadocJar)

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = 'net.william278'
                    artifactId = 'papiproxybridge'
                    version = "$rootProject.version"
                    artifact jar
                    artifact javadocJar
                    artifact sourcesJar
                }
            }
        }
    }

    if (['bukkit', 'bungee', 'velocity'].contains(project.name) || project.parent?.name?.equals('fabric')) {
        shadowJar {
            destinationDirectory.set(file("$rootDir/target"))
            archiveClassifier.set('')
        }
    }

    jar.dependsOn(shadowJar)
    clean.delete "$rootDir/target"
}

logger.lifecycle("Building PAPIProxyBridge ${version} by William278")

@SuppressWarnings('GrMethodMayBeStatic')
def versionMetadata() {
    // Get if there is a tag for this commit
    def tag = grgit.tag.list().find { it.commit.id == grgit.head().id }
    if (tag != null) {
        return ''
    }

    // Otherwise, get the last commit hash and if it's a clean head
    if (grgit == null) {
        return '-' + System.getenv("GITHUB_RUN_NUMBER") ? 'build.' + System.getenv("GITHUB_RUN_NUMBER") : 'unknown'
    }
    return '-' + grgit.head().abbreviatedId + (grgit.status().clean ? '' : '-indev')
}