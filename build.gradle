import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'io.github.goooler.shadow' version "8.1.8"
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'org.ajoberstar.grgit' version '5.2.2'
    id 'maven-publish'
    id 'java'
}

group 'net.william278'
version "$ext.plugin_version${versionMetadata()}"
description "$ext.plugin_description"
defaultTasks 'licenseFormat', 'build'

ext {
    set 'version', version.toString()
    set 'description', description.toString()
    set 'fabric_minecraft_version', minecraft_version.toString()
    set 'fabric_yarn_mappings', yarn_mappings.toString()
    set 'fabric_loader_version', loader_version.toString()
    set 'fabric_api_version', fabric_version.toString()
}

allprojects {
    apply plugin: 'io.github.goooler.shadow'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'java'

    version = "$ext.plugin_version${versionMetadata()}"

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.release.set 17
    javadoc.options.encoding = 'UTF-8'
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.fabricmc.net/' } // Fabric maven is above paper maven to get FabricAPi from it over PaperMC
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.2'
    }

    test {
        useJUnitPlatform()
    }

    license {
        header = rootProject.file('HEADER')
        include '**/*.java'
        newLine = true
    }

    processResources {
        filesMatching(['**/*.json', '**/*.yml']) {
            filter ReplaceTokens as Class, beginToken: '${', endToken: '}',
                    tokens: rootProject.ext.properties
        }
    }

    jar {
        manifest {
            attributes 'Implementation-Version': version
        }
    }
}

subprojects {
    version rootProject.version
    archivesBaseName = "${rootProject.name}-${project.name.capitalize()}"

    if (['common'].contains(project.name)) {
        java {
            withSourcesJar()
            withJavadocJar()
        }

        sourcesJar {
            destinationDirectory.set(file("$rootDir/target"))
        }
        javadocJar {
            destinationDirectory.set(file("$rootDir/target"))
        }
        jar.dependsOn(sourcesJar, javadocJar)

        publishing {
            repositories {
                if (System.getenv("RELEASES_MAVEN_USERNAME") != null) {
                    maven {
                        name = "william278-releases"
                        url = "https://repo.william278.net/releases"
                        credentials {
                            username = System.getenv("RELEASES_MAVEN_USERNAME")
                            password = System.getenv("RELEASES_MAVEN_PASSWORD")
                        }
                        authentication {
                            basic(BasicAuthentication)
                        }
                    }
                }
                if (System.getenv("SNAPSHOTS_MAVEN_USERNAME") != null) {
                    maven {
                        name = "william278-snapshots"
                        url = "https://repo.william278.net/snapshots"
                        credentials {
                            username = System.getenv("SNAPSHOTS_MAVEN_USERNAME")
                            password = System.getenv("SNAPSHOTS_MAVEN_PASSWORD")
                        }
                        authentication {
                            basic(BasicAuthentication)
                        }
                    }
                }

                publications {
                    mavenJava(MavenPublication) {
                        groupId = 'net.william278'
                        artifactId = 'papiproxybridge'
                        version = "$rootProject.version"
                        artifact jar
                        artifact javadocJar
                        artifact sourcesJar
                    }
                }
            }
        }
    }

    if (['bukkit', 'bungee', 'velocity', 'plugin'].contains(project.name)) {
        shadowJar {
            destinationDirectory.set(file("$rootDir/target"))
            archiveClassifier.set('')
        }

        jar.dependsOn shadowJar
        clean.delete "$rootDir/target"
    }
}

logger.lifecycle("Building PAPIProxyBridge ${version} by William278")

@SuppressWarnings('GrMethodMayBeStatic')
def versionMetadata() {
    // Get if there is a tag for this commit
    def tag = grgit.tag.list().find { it.commit.id == grgit.head().id }
    if (tag != null) {
        return ''
    }

    // Otherwise, get the last commit hash and if it's a clean head
    if (grgit == null) {
        return '-' + System.getenv("GITHUB_RUN_NUMBER") ? 'build.' + System.getenv("GITHUB_RUN_NUMBER") : 'unknown'
    }
    return '-' + grgit.head().abbreviatedId + (grgit.status().clean ? '' : '-indev')
}