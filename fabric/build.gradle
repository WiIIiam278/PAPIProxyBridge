plugins {
    id 'gg.essential.multi-version'
    id 'gg.essential.defaults'
}

loom.serverOnlyMinecraftJar()

repositories {
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://maven.nucleoid.xyz' } // PlaceholderAPI
}

dependencies {
    shadow implementation ('io.lettuce:lettuce-core:6.5.5.RELEASE')
    shadow implementation ('de.exlll:configlib-yaml:4.6.1')

    modImplementation include("net.kyori:adventure-platform-fabric:${fabric_adventure_platform_version}")
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    modImplementation include("eu.pb4:placeholder-api:${fabric_placeholder_api_version}")

    compileOnly "net.kyori:adventure-text-serializer-gson:4.19.0"

    shadow project(path: ":common")
}

processResources {
    filesMatching(Arrays.asList("fabric.mod.json")) {
        expand([
                version: version,
                fabric_loader_version: fabric_loader_version,
                fabric_minecraft_version: project.name,
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    destinationDirectory.set(file("$projectDir/build/libs"))

    exclude('net.fabricmc:.*')
    exclude('net.kyori:.*')
    exclude '/mappings/*'

    relocate 'net.jodah', 'net.william278.papiproxybridge.libraries'
    relocate 'org.snakeyaml', 'net.william278.papiproxybridge.libraries.snakeyaml'
    relocate 'io.lettuce', 'net.william278.papiproxybridge.libraries.lettuce'
    relocate 'de.exlll', 'net.william278.papiproxybridge.libraries.configlib'
    relocate 'org.reactivestreams', 'net.william278.papiproxybridge.libraries.reactivestreams'
    relocate 'reactor', 'net.william278.papiproxybridge.libraries.reactor'

    minimize()
}

remapJar {
    dependsOn tasks.shadowJar
    mustRunAfter tasks.shadowJar
    inputFile = shadowJar.archiveFile.get()
    addNestedDependencies = true

    destinationDirectory.set(file("$rootDir/target/"))
    archiveClassifier.set('')
}

shadowJar.finalizedBy(remapJar)