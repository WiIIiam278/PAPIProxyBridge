plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
}

apply plugin: 'fabric-loom'
loom.serverOnlyMinecraftJar()

repositories {
    mavenCentral()
    maven { url 'https://maven.nucleoid.xyz' } // PlaceholderAPI
}

dependencies {
    minecraft "com.mojang:minecraft:$fabric_minecraft_version"
    mappings "net.fabricmc:yarn:$fabric_yarn_mappings:v2"

    modCompileOnly "net.fabricmc:fabric-loader:$fabric_loader_version"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"
    modCompileOnly "eu.pb4:placeholder-api:2.3.0+"

    modImplementation include("net.kyori:adventure-api:4.14.0")

    compileOnly "net.kyori:adventure-text-serializer-gson:4.15.0"
    shadow project(path: ":common")
}

shadowJar {
    configurations = [project.configurations.shadow]

    exclude('net.fabricmc:.*')
    exclude('net.kyori:.*')
    exclude '/mappings/*'

    relocate 'org.jetbrains', 'net.william278.papiproxybridge.libraries'
    relocate 'org.intellij', 'net.william278.papiproxybridge.libraries'
    relocate 'net.jodah', 'net.william278.papiproxybridge.libraries'

    minimize()
}

remapJar {
    dependsOn tasks.shadowJar
    mustRunAfter tasks.shadowJar
    inputFile = shadowJar.archiveFile.get()
    addNestedDependencies = true

    destinationDirectory.set(file("$rootDir/target/"))
    archiveClassifier.set('')
}

shadowJar.finalizedBy(remapJar)